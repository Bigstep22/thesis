\subsection{Haskell's optimization pipeline}
In order to understand how fusion works, it is important to understand a few other concepts with which fusion works in tandem.
Namely, beta reduction, inlining, case-of-case optimizations, and tail call optimization.
We will discuss a brief description of each.

Repeating my research question, the first sub-point should be answered here:
To implement (Co)Church encodings, what is necessary to make the code reliably fuse? This leads to the following sub-questions:
\begin{itemize}[noitemsep]
  \item What optimizations are present in Haskell that enable fusion to work?
  \item What tools and techniques are available to get Haskell's compiler to cooperate and trigger fusion?
\end{itemize}

\subsubsection{Beta reduction}
Beta reduction is simply the rule where an expression of the form \tt{($\lambda$ x . a[x]) y} can get transformed into \tt{a[y]}.
For example \tt{($\lambda$ x . x + x) y} would become \tt{y + y}.
\subsubsection{Inlining}
Inlining is the process of taking a function expression and unfolding it into its definition.
If we take the function \tt{f = (+2)} and an expression \tt{f 5}, we could inline \tt{f} such that we get \tt{(+2) 5}; which we could inline again to obtain \tt{5 + 2}.
\subsubsection{Case of case, and known-case elimination}
As discussed by \cite{Jones1996}, case of case optimization is the transformation of the following pattern:
\begin{spec}
case ( 
  case C of 
    B1 -> F1
    B2 -> F2
  ) of
  A1 -> E1
  A2 -> E2
\end{spec}
To the following\footnote{This specific example was retrieved from: \url{https://stackoverflow.com/questions/35815503/what-ghc-optimization-is-responsible-for-duplicating-case-expressions}}:
\begin{spec}
case C of    
  B1 -> case F1 of
    A1 -> E1
    A2 -> E2
  B2 -> case F2 of
    A1 -> E1
    A2 -> E2
\end{spec}
Where the branches of the outer case can be pushed into the branches of the inner.
Furthermore:
\begin{spec}
case V of
  V -> Expr
  ...
\end{spec}
Can be simplified by case-of-known-constructor optimization to:
\begin{spec}
Expr
\end{spec}
Together, these optimizations can often lead to the removal of unnecessary computations. Take as an example \citep{Jones1996}:
\begin{spec}
if (not x) then E1 else E2
\end{spec}
``No decent compiler would actually negate the value of \textbf{x} at runtime! [\ldots] After desugaring the conditional, and inlining the definition of \textbf{not}, we get'' \citep{Jones1996}:
\begin{spec}
case (case x of
  True -> False
  False -> True
) of 
  True -> E1 
  False -> E2
\end{spec}
With case-of-case transformation this gets transformed to:
\begin{spec}
case x of 
  True -> case False of
    True -> El
    False -> E2
  False -> case True of
    True -> El
    False -> E2
\end{spec}
Then the case-of-known-constructor transformation gets us:
\begin{spec}
case x of
  True -> E2
  False -> E1
\end{spec}
No more runtime evaluation of not!
% To see an example of these optimizations in action, see \autoref{app:cochurch_stream}.

\paragraph{Tail Recursion}\label{sec:tail}
\subparagraph{Definition}
We call a recursive function tail-recursive, if all its recursive calls return immediately upon completion i.e., they don't do any additional calculations upon the result of the recursive call before returning a result.

When a function is tail-recursive, it is possible to reuse the stack frame of the current function call, reducing a lot of memory overhead, only needing to execute make a jump each time a recursive `call' is made.
Haskell is able to identify tail-recursive functions and optimize the compiled byte code accordingly.

\paragraph{Example}
The following code, when applying fusion, case-of-case, and case-of-known-case optimization:
\begin{spec}
sumCoCh . mapCoCh (+2) . filterCoCh odd . ListCoCh betweenCoCh
\end{spec}
Reduces to (See \autoref{app:cochurch_stream} for derivation):
\begin{spec}
loop (x, y) = if (x > y)
              then 0
              else if (odd x)
                   then (x + 2) + loop (x+1, y)
                   else loop (x+1, y)
loop (x, y)
\end{spec}
This definition is not tail recursive as the \tt{then (x + 2) + loop (x+1, y)} line includes some calculations that still need to made upon completion of the recursive \tt{loop} call; i.e., the \tt{loop} function is not in tail position.

If we tweak the definition of sum, such that it is tail recursive we get a different derivation (See \autoref{app:cochurch_tail} for derivation):
\begin{spec}
sumCoCh . mapCoCh (+2) . filterCoCh odd . ListCoCh betweenCoCh
\end{spec}
Reduces to:
\begin{spec}
loop (x, y) acc = if (x > y)
                  then acc
                  else if (odd x)
                       then loop (x+1, y) ((x + 2) + acc)
                       else loop (x+1, y)
loop (x, y) 0
\end{spec}
Which is identical except for the fact that \tt{loop} is tail-recursive.
All that has been changed is the recursion principle \tt{su'}.

Cochurch encodings better lend themselves to having fully tail-recursive fused pipelines, as writing a coinduction principle that is tail-recursive is easier than writing a recursion principle that is.
For a further discussion on this, see \cite{Breitner2018}'s work and \autoref{sec:hask_perf}.


\subsubsection{Performance Considerations}\label{sec:hs_perf}
We discuss many different considerations and details when optimizing the fusible code.
This discussion is summarized here.
In order to make sure a pipeline of functions fuses in Haskell, there are several things that need to be taken into consideration:
\begin{itemize}[noitemsep]
    \item Make sure you only pass through parameters that change between recursive calls. Instead of writing:
    \begin{spec}
b a (x, y) = loop x y
  where loop x y = if x > y
                   then a Nil_
                   else a (Cons_ x (loop (x+1) y))
    \end{spec}
    Where the \tt{y} doesn't change between calls of \tt{loop}, modify \tt{loop} such that it doesn't pass through the \tt{y}:
    \begin{spec}
b a (x, y) = loop x
  where loop x = if x > y
                 then a Nil_
                 else a (Cons_ x (loop (x+1)))
    \end{spec}
    This way, fewer data need to be pushed around in memory for each (recursive) function call.
    \item Ensure that functions are inlined properly. So for the second example above add a pragma that inlines the function.
    This ensures that other pragmas, that  do the actual fusion, can fire during the compilation process.
    \begin{spec}
        {-# INLINE betweenCh #-}
    \end{spec}
    \item Ensure that the fused result is tail recursive.
    For consumer functions, it is often possible to make the function tail recursive.
    For the corecursion principle of sum \tt{su}:
    \begin{spec}
su :: (s -> List_ Int s) -> s -> Int
su h s = loop s
  where loop s' = case h s' of
          Nil_ -> 0
          Cons_ x xs -> x + loop xs
    \end{spec}
    It is possible to modify the definition of the corecursion \tt{loop} such that it is tail-recursive:
    \begin{spec}
su :: (s -> List_ Int s) -> s -> Int
su h s = loopt s 0
  where loopt s' sum = case h s' of
          Nil_ -> sum
          Cons_ x xs -> loopt xs (x + sum)
    \end{spec}
For Church encodings, it is a little more tricky to get the resultant function to be tail-recursive, it is possible, however.
Taking the algebra for sum again:
    \begin{spec}
sum2 :: List' Int -> Int
sum2 = sumCh . toCh
  where sumCh :: ListCh Int -> Int
        sumCh (ListCh g) = g su
        su :: List_ Int Int -> Int
        su Nil_ = 0
        su (Cons_ x y) = x + y
\end{spec}
We can modify the type of the recursive part of list and the return type to be a function instead of just a simple datatype (\tt{Int -> Int} instead of \tt{Int}):
\begin{spec}
sum7 :: List' Int -> Int
sum7 = flip sumCh 0 . toCh
  where sumCh :: ListCh Int -> (Int -> Int)
        sumCh (ListCh g) = g su
        su :: List_ Int (Int -> Int) -> (Int -> Int)
        su Nil_ s = s
        su (Cons_ x y) s = y (s + x)
    \end{spec}

\cite{Breitner2018} introduced and subsequently make possible this optimization in Haskell.
\item Ensure that the fused result is a single recursive function (so no helper functions such as \tt{go}).
This was a problem when writing the filter function in Cochurch encoded fashion.
This is only possible if a recursive natural transformation function is used, but it unfortunately does not fuse.
This is due to \tt{go} being a recursive function, which Haskell does not inline on its own.

The current workaround for this is Stream fusion as described by \cite{Coutts2007}.
Adding a Skip constructor makes a big difference to enabling the avoidance of recursive functions.
This workaround is part of my performance analysis.
\end{itemize}


